import groovy.sql.Sql
import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os

group 'com.limado'

Properties rootProps = new Properties()
rootProps.load(new FileInputStream(rootProject.projectDir.absolutePath + "/gradle.properties"))
rootProps.each{key, value -> rootProject.ext.set(key, value)}
ext {
    webapps_dir="$jetty_base_dir/webapps"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply from: '../mvn.gradle'
    apply from: '../gretty.plugin'
    apply plugin: "jacoco"
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    //apply plugin: 'checkstyle'

    rootProps.each{key, value -> project.ext.set(key, value)}
    File subProjectPropsFile = new File(project.projectDir.absolutePath + '/gradle.properties');
    if(subProjectPropsFile.exists()) {
        Properties subProjectProps = new Properties()
        subProjectProps.load(new FileInputStream(subProjectPropsFile))
        subProjectProps.each{key, value -> project.ext.set(key, value)}
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.21'
        testCompile 'junit:junit:4.12'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    processResources {
        with copySpec {
            from project.projectDir.absolutePath + '/src/main/resources'
            include '**/*.properties'
            include '**/*.xml'
            filter(ReplaceTokens, tokens: project.ext.properties)
        }
        exclude 'sql'
    }

    processTestResources {
        with copySpec {
            from project.projectDir.absolutePath + '/src/test/resources'
            include '**/*.properties'
            include '**/*.xml'
            filter(ReplaceTokens, tokens: project.ext.properties)
        }
    }

    buildscript {
        repositories {
            jcenter()
        }

        dependencies {
            fileTree(dir: rootProject.projectDir.absolutePath + "/db", include: '*.jar')
        }

        URLClassLoader loader = groovy.sql.Sql.class.classLoader
        project.buildscript.configurations.classpath.each { File file ->
            loader.addURL(file.toURL())
        }
    }

    task createTables {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                def sql = Sql.newInstance("$db_url", "$db_user", "$db_password", "$db_driverClass")
                executeSQL(sql, new File(project.projectDir.absolutePath + '/src/main/resources/sql/create.sql'))
            }
        }
    }

    task dropTables {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                def sql = Sql.newInstance("$db_url", "$db_user", "$db_password", "$db_driverClass")
                executeSQL(sql, new File(project.projectDir.absolutePath + '/src/main/resources/sql/drop.sql'))
            }
        }
    }

    task createTestTables {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                def sql = Sql.newInstance("$test_db_url", "$test_db_user", "$test_db_password", "$test_db_driverClass")
                executeSQL(sql, new File(project.projectDir.absolutePath + '/src/main/resources/sql/create.sql'))
            }
        }
    }

    task dropTestTables {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                def sql = Sql.newInstance("$test_db_url", "$test_db_user", "$test_db_password", "$test_db_driverClass")
                executeSQL(sql, new File(project.projectDir.absolutePath + '/src/main/resources/sql/drop.sql'))
            }
        }
    }

    task updateDB {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                ant.taskdef(name: 'dbdeploy',
                        classname: 'com.dbdeploy.AntTarget',
                        classpath: fileTree(dir: rootProject.projectDir.absolutePath + "/db", include: '*.jar').asPath)

                ant.dbdeploy(driver: "$db_driverClass",
                        url: "$db_url",
                        userid: "$db_user",
                        password: "$db_password",
                        dbms: "mysql",
                        dir: project.projectDir.absolutePath + '/src/main/resources/sql/patch',
                        undooutputfile: project.projectDir.absolutePath + '/src/main/resources/sql/patch/undo_last_change.sql')
            }
        }
    }

    task undoDB {
        doLast {
            if ("$has_dbTasks".toBoolean()) {
                def sql = Sql.newInstance("$db_url", "$db_user", "$db_password", "$db_driverClass")
                executeSQL(sql, new File(project.projectDir.absolutePath + '/src/main/resources/sql/patch/undo_last_change.sql'))
            }
        }
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = false
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled true
            html.enabled false
        }
        reportLevel = "medium"
        ignoreFailures = "$findbugs_ignoreFailures"
    }

    pmd {
        ignoreFailures = "$pmd_ignoreFailures"
    }

    // ========== Dev ==========
    /*
    checkstyle {
        toolVersion = "7.1.1"
    }
    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    gretty {
        //http://akhikhl.github.io/gretty-doc/Gretty-configuration.html
        servletContainer = 'jetty9'
        httpPort = 80
        jvmArgs = ['-Xms512M', '-Xmx512M', '-XX:MaxPermSize=128M']
        realm 'auth'
        realmConfigFile project.projectDir.absolutePath + '/dev/realm.properties'
        logDir = 'D:/log'
        redeployMode = 'restart'
        scanInterval = 1
    }
    */
}

def executeSQL(groovy.sql.Sql sql, File file) {
    file.eachLine {
        if (!it.startsWith("--") && !it.isEmpty()) {
            try {
                sql.execute(it)
            } catch (Throwable e) {
                println e;
                if (!"$dbscript_ignoreFailures".toBoolean()) {
                    throw e
                }
            }
        }
    }
}

// ========== DB ==========
task createChangelogTable {
    doLast {
        def sql = Sql.newInstance("$db_url", "$db_user", "$db_password", "$db_driverClass")
        executeSQL(sql, new File(rootProject.projectDir.absolutePath + '/db/create_changelog_table.sql'))
    }
}

task dropChangelogTable {
    doLast {
        def sql = Sql.newInstance("$db_url", "$db_user", "$db_password", "$db_driverClass")
        executeSQL(sql, new File(rootProject.projectDir.absolutePath + '/db/drop_changelog_table.sql'))
    }
}

task initDB(dependsOn: [createChangelogTable, rootProject.subprojects.createTables])

task cleanDB(dependsOn: [dropChangelogTable, rootProject.subprojects.dropTables])

// ========== Git ==========
task deployGitVersionFile(type:Exec) {
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        workingDir '.'
        commandLine 'git', 'describe'
        standardOutput = new FileOutputStream('version.txt')
    }
    else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir '.'
        commandLine 'cmd', '/c', 'git', 'describe'
        standardOutput = new FileOutputStream('version.txt')
    }
    doLast {
        copy {
            from 'version.txt'
            into "$webapps_dir/ROOT"
        }
    }
}

// ========== Jetty ==========
task configureJetty {
    doLast {
        if (Os.isFamily(Os.FAMILY_UNIX)) {
            println 'linux: copy jetty.sh'
            copy {
                from 'config/jetty'
                into "$jetty_home"
                include 'bin/jetty.sh'
                filter(ReplaceTokens, tokens:rootProject.ext.properties)
            }
        }
        else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            println 'windows: copy jetty.bat'
            copy {
                from 'config/jetty'
                into "$jetty_home"
                include 'bin/jetty*.bat'
                filter(ReplaceTokens, tokens:rootProject.ext.properties)
            }
        }
    }
}

task configureJettyBase {
    doLast {
        copy {
            from 'config/jetty-base'
            into "$jetty_base_dir"
            include '**/*.ini'
            include '**/*.properties'
            filter(ReplaceTokens, tokens: rootProject.ext.properties)
        }
    }
}

task gensslkey {
    doLast {
        println "alias: $ssl_alias"
        println "keystore: $ssl_keystore_path"
        println "dname: $ssl_dname"
        delete "$ssl_keystore_path"
        ant.genkey(alias: "$ssl_alias", keystore: "$ssl_keystore_path", storepass: "$ssl_storepass", dname: "$ssl_dname", validity: '36500', keysize: '2048', keyalg: 'RSA')
    }
}